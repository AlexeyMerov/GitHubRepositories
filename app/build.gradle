apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply from: 'build-versioning.gradle'


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.alexeymerov.githubrepositories"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode buildVersionCode
        versionName buildVersionName
    }
    buildTypes {
        release {
            debuggable false
            shrinkResources = true
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildOutputApkName(applicationVariants)
        }
        debug {
            buildOutputApkName(applicationVariants)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
//        lintConfig file("lint.xml")
//        baseline file("baseline-lint.xml")
//        warningsAsErrors = true
    }

    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
}

def buildOutputApkName(DomainObjectSet domainObjectSet) {
    domainObjectSet.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "GHS_${variant.baseName}_${buildVersionName}_" + new Date().format('(dd.MM.yy)_(HH)') + ".apk"
        }
    }
}

ext {
    retrofit_version = '2.3.0'
    anko_version = '0.10.4'
    glide_version = '4.8.0'
    lifecycle_version = '2.1.0-alpha03'
    dagger_version = '2.22.1'
    version_room = '2.1.0-alpha06'
    firebase_room = '4.3.1'
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'

    debugImplementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'

    // android
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation "androidx.recyclerview:recyclerview:1.1.0-alpha04"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "com.google.android.material:material:1.1.0-alpha05"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.0'

    // rx libs
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    // di
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha04'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:2.1.0-alpha04"

    // Room
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.room:room-rxjava2:$version_room"
    annotationProcessor "androidx.room:room-compiler:$version_room"
    kapt "androidx.room:room-compiler:$version_room"

    //permissions with dsl
    implementation 'com.github.fondesa:kpermissions:1.0.0'

    // anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    // glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Firebase
    implementation "com.firebaseui:firebase-ui-auth:$firebase_room"
    implementation "com.firebaseui:firebase-ui-auth-github:$firebase_room"

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'


}

apply plugin: 'com.google.gms.google-services'